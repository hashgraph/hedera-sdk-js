export PATH := "./node_modules/.bin:" + env_var('PATH')

default:
    just --choose

install:
    pnpm i

docs:
    typedoc --excludeInternal --excludePrivate --excludeProtected --out docs --validation.invalidLink --entryPoints src/index.js src/browser.js src/native.js

build:
    pbjs -r hashgraph -t static-module -w es6 --force-long --no-beautify --no-convert --no-delimited --no-verify -o src/proto.js src/proto/*/**.proto
    pbts -n hashgraph -o src/proto.d.ts src/proto.js
    perl -pi -e "s#(?<!api\.)proto\.#hashgraph.proto.#g" src/proto.d.ts
    babel src -d lib
    copyfiles -u 1 src/index.d.ts src/proto.d.ts lib/

update:
    cd src/proto && git pull origin main

lint: lint-types lint-dpdm lint-format lint-js

lint-types:
    tsc

lint-dpdm:
    dpdm src/index.js --circular true --tree false --warning false > /dev/null

lint-format:
    prettier src "test/unit/*.js" "test/integration/*.js" "*.json" "src/*.js" --check > /dev/null

lint-js:
    eslint --fix "src/**/*.js" "test/integration/**/*.js" "test/unit/**/*.js"

format:
    prettier src "test/unit/*.js" "test/integration/*.js" "*.json" "src/*.js" --write > /dev/null
